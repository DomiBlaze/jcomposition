group 'trollsoftware'
version '1.1.1-SNAPSHOT'

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.5

repositories {
    mavenCentral()
    maven{ url 'https://jitpack.io' }
}

sourceSets {
    generated {
        java {
            srcDir 'src/main/generated'
        }
    }
}

compileJava {
    options.compilerArgs += [
            '-s', 'src/main/generated'
    ]

    doFirst {
        file('src/main/generated').mkdir()
    }
}

dependencies {
    compile project(':processor')

    // For sample
    compile group: 'com.google.dagger', name: 'dagger', version: '2.8'
    compile group: 'com.google.dagger', name: 'dagger-compiler', version: '2.8'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
        archives jar
    }

    // To specify a license in the pom:
    install {
        repositories.mavenInstaller {
            pom.project {
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}